buildscript {
    repositories.gradlePluginPortal()
    dependencies.classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.21.2"
}

apply plugin: 'java-library'

group 'org.comroid'
version '1.5.1-SNAPSHOT'

System.out.println("##teamcity[buildNumber '$version']")

apply from: 'gradle/vars.gradle'

sourceCompatibility = targetCompatibility = 1.8

tasks.findByName('wrapper').configure {
    gradleVersion = '6.0'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"   
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    
    try {
        archiveClassifier.set 'sources'
    } catch (MissingPropertyException ignored) {
        classifier = "sources"
    }
}

task javadocJar(type: Jar) {
    dependsOn 'javadoc'
    
    from javadoc.destinationDir

    try {
        archiveClassifier.set 'javadoc'
    } catch (MissingPropertyException ignored) {
        classifier = "javadoc"
    }
}

javadoc {
    source = sourceSets.main.java
    
    options {
        encoding = 'UTF-8'
        destinationDirectory(file("./docs/"))
        
        /*links = [
                "https://docs.oracle.com/javase/8/docs/api/",
                "https://docs.oracle.com/javaee/7/api/"
        ]*/
    }
}

compileJava.options.encoding = 'UTF-8'
repositories.jcenter()

dependencies {
    api 'org.apache.logging.log4j:log4j-api:2.13.0'

    compileOnly 'org.javacord:javacord-api:3.0.5'
    compileOnly 'org.javacord:javacord-core:3.0.5'
    compileOnly 'com.fasterxml.jackson.core:jackson-databind:2.10.1'

    compileOnly 'org.jetbrains:annotations:18.0.0'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.easymock:easymock:4.1'
    testImplementation 'org.javacord:javacord:3.0.4'

    testCompileOnly 'org.codehaus.groovy:groovy:3.0.0-rc-2'
}

sourceSets {
    main.java.srcDirs = ["src\\main\\java"]
    test.java.srcDirs = ["src\\test\\java"]
}

apply from: 'gradle/publishing.gradle'
